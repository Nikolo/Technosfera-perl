\setcounter{chapter}{2}
\chapter{Модульность и повторное использование}
Данная лекция посвящена модульности в языке Perl. Знания базового синтаксиса языка программирования недостаточно, чтобы писать сложные законченные программные продукты, поскольку любой полноценный проект состоит из множества модулей. Построение сложной иерархии проекта с технической и логической точек зрения является темой данной лекции.

\section{Команды типа <<\texttt{include}>>} %1 (1:21)

\subsection{Функция \texttt{eval}}
В языке C команда \verb|include| позволяет подключить другой файл с кодом с помощью <<механической>> подстановки его содержимого. Эта команда позволяет разбивать сложные проекты на несколько файлов.
Похожие команды есть во многих других языках программирования.

Самый простой способ исполнить некоторый код в языке Perl~--- использование функции \verb|eval|. Если передать этой функции строку с кодом, то этот код будет исполнен.

Важной особенностью является то, что \verb|eval| создает свою область видимости, а следовательно, локальные переменные, объявленные с помощью \verb|my|, будут ограничены функцией \verb|eval|. Это можно заметить в приведенном выше примере: переменная \verb|$y| объявляется с помощью \verb|my| и существует только внутри \verb|eval|, а переменная \verb|$u|, объявленная вне \verb|eval|, внутри \verb|eval| изменяет свое значение.

Этот способ прост, но требует выполнения множества дополнительных действий вручную. К таким действиям относятся, например, чтение файлов и обработка ошибок.

\subsection{Функция \texttt{do}} %3 (3:05)
\verb|do|~--- более совершенная версия \verb|eval|. Не следует путать этот \verb|do| с тем \verb|do|, который используется для создания циклов.
Функция \verb|do| принимает имя файла, содержимое которого она
считывает и исполняет с помощью \verb|eval|.

Функция \verb|do|, как и \verb|eval|, создает свою область видимости.
Однако \verb|do| практически не используется в сложных проектах, так как существует более высокоуровневая функция \verb|require|.

\subsection{Функция \texttt{require}} %4 (3:58)
Функция \verb|require|~--- более совершенная высокоуровневая форма \verb|do|.
Ей также нужно передать имя файла, после чего произойдет импорт и последующее исполнение кода из файла.
Однако функция \verb|require| имеет некоторые особенности.

Во-первых, \verb|require| поддерживает синтаксис с использованием двойного двоеточия, что позволяет абстрагироваться от реальных имен файлов и давать модулям названия.
В следующем примере первым вызовом \verb|require| будет загружен файл с расширением \verb|.pl|,
а вторым вызовом~--- файл с расширением \verb|.pm| (perl module):

Во-вторых, функция \verb|require| проверяет, что код модуля после выполнения возвращает истинное значение. Поэтому основная масса модулей заканчивается строчкой <<\verb|1;|>>:
\inputminted
  [frame=leftline,linenos,firstline=1]
  {perl}
  {lectures/L3/L3-example-include-require-sqr.pl}

Такой подход позволяет гарантировать, что последнее выражение в модуле истинно и функция \verb|require| сочтет такой модуль успешно загруженным.
В очень малом числе случаев модулю действительно нужно сообщить, успешно ли он загружен~--- в таких случаях используются специальные проверки.

Синтаксис с двойными двоеточиями позволяет указать путь до модуля

Данный код сработает вне зависимости от операционной системы и используемого в ней разделителя каталогов.

Поиск модулей \verb|require| выполняет в каталогах, содержащихся в массиве
\verb|@INC| (на самом деле функция \verb|do| делает то же самое)

Добавить каталог в этот массив (для того, чтобы \verb|require| искал модуль и в нем) можно следующими способами:
\begin{enumerate}
  \item Добавив каталог в переменную окружения \verb|PERL5LIB|:
    \begin{minted}[frame=leftline,linenos]{bash}
$ PERL5LIB=/tmp/lib perl ...
    \end{minted}
  \item Используя ключ \verb|-I| интерпретатора:
    \begin{minted}[frame=leftline,linenos]{bash}
$ perl -I /tmp/lib ...
    \end{minted}
\end{enumerate}
Помимо указанных способов, можно также явно модифицировать массив
\verb|@INC|, но такой подход очень редко оправдан.

На данный момент рассмотрены основные методы подключения модулей.
Существует, однако, ещё один способ, о котором будет сказано позднее.

% ------------------------------------------------------
\section{Блоки фаз} %7:56
В \verb|perl| сушествует возможность указать блок \verb|BEGIN|, который будет исполнен в начале программы вне зависимости от реального расположения внутри программы:
\begin{minted}{perl}
BEGIN {
  require Some::Module;
}

sub test1 {
  return 'test1';

* sub test2 {
*   return 'test2';
*
*   BEGIN {...}
* }
}
\end{minted}
Важной особенностью \verb|perl| является то, что функции объявляются еще до исполнения программы. В данном примере будет сначала выполнен первый блок \verb|BEGIN|, потом объявлены функции $test1$ и $test2$, выполнен, вложенный в функцию $test2$, блок \verb|BEGIN| и только после этого начнется исполнение программы.

Парный блоку \verb|BEGIN|, блок \verb|END| исполняется, наоборот, когда программа завершилась:
\begin{minted}{perl}
open(my $fh, '>', $file);

while (1) {
  # ...
}

END {
  close($fh);
  unlink($file);
}
\end{minted}
Он исполняется последним вне зависимости от расположения в исходном коде программы. Чаще всего блок \verb|END| используется для очистки ресурсов. В данном примере в блоке \verb|END| реализован процесс завершения работы с файлом.

Также в \verb|perl| существуют блоки:
\begin{itemize}
	\item CHECK\{\}
	\item UNITCHECK\{\}
	\item INIT\{\}
\end{itemize}
Такое большое количество разнообразных блоков фаз связано с работой интерпретатора. Использование нужного блока позволяет исполнить требуемый код в нужный момент работы интерпретатора. Эти особенности далее обсуждаться не будут. В реальном коде данные блоки встречаются крайне редко.

Внутри всех блоков присутствует переменная
\[ \$\{ \textasciicircum GLOBAL \_ PHASE \}, \]
в которой хранится название текущей фазы (\verb|INIT|, \verb|UNITCHECK| и т.п.).

\section{Команды типа <<include>> (продолжение)} %10 (11:20)
Использование ключевого слова \verb|use|~--- основной способ подключения модулей в \verb|perl|, которы представляет собой выполнение \verb|require| внутри блока \verb|BEGIN|. Модули подключаются в заданном порядке.
\begin{minted}{perl}
use My_module;     # My_module.pm
use Data::Dumper;  # Data/Dumper.pm
BEGIN { push(@INC, '/tmp/lib'); }
use Local::Module; # Local/Module.pm
\end{minted}
В данном случае сначала будут подключены два модуля, затем выполнен блок \verb|BEGIN|, а после~--- подключен третий модуль.

Как и \verb|require|, \verb|use| умеет понимать литералы с двойными двоеточиями.

Выполнить \verb|use| можно используя ключ интерпретатора $-M$.

\section{Пространства имен} %11 (12:19)

\begin{minted}{perl}
require Some::Module;
function(); # ?

require Another::Module;
another_function(); # ??

require Another::Module2;
another_function(); # again!?
\end{minted}

В \verb|perl| пространства имён (англ. \verb|namespace|) называются пакетами (англ. \verb|package|). С помощью пакетов можно создать отдельную область видимости для функций и переменных так, чтобы они не были доступны извне по свои коротким именам, но доступны по $full$ $qualified$ $name$.

Ключевое слово \verb|package| используется для объявления пакета и все объявленные функции и переменные до конца области видимости будут входить в этот пакет.
Для имен пакетов используется такой же синтаксис с двумя двоеточиями, который встречался ранее. Это сделано не случайно~--- существует соглашение внутри каждого модуля определять пакет с точно таким же именем. Это позволяет удобно организовать код программы.
\begin{minted}{perl}
require Some::Module;
Some::Module::function();

require Another::Module;
Another::Module::another_function();

require Another::Module2;
Another::Module2::another_function(); # np!
\end{minted}

Например, в следующем примере после подключения модуля Local::Multiplier
\begin{minted}{perl}
use Local::Multiplier;

print Local::Multiplier::m3(8); # 24
\end{minted}
имеется возможность использовать функции, объявленные в одноимённом пакете:
\begin{minted}{perl}
package Local::Multiplier;

sub m2 {
  my ($x) = @_;
  return $x * 2;
}

sub m3 {
  my ($x) = @_;
  return $x * 3;
}
\end{minted}
Имена функций отделяются от имени пакета также двойным двоеточием.

Ключевое слово \verb|package| не обязательно указывать в начале файла. Оно может быть использовано в любом месте и помещает переменные и функции в пакет до конца области видимости. Сразу после этого пакет становится доступен. Например:
\begin{minted}{perl}
{
  package Multiplier;
  sub m_4 { return shift() * 4 }
}

print Multiplier::m_4(8); # 32
\end{minted}
 Имя пакета можно получить используя ключевое слово \verb|__PACKAGE__|:
\begin{minted}{perl}
package Some::Module::Lala;

print __PACKAGE__; # Some::Module::Lala
\end{minted}

\section{Переменные пакета}
Переменные пакета объявляются с помощью ключевого слова \verb|our| (а не \verb|my|) и внутри пакета доступны по короткому имени. К переменным пакета можно обращаться всегда и по длинному имени, но это часто не удобно (например, при переименовании пакета пришлось бы переименовывать все его переменные).
\begin{minted}{perl}
{
  package Some;
  my $x = 1;
  our $y = 2; # $Some::y;

  our @array = qw(foo bar baz);
}

print $Some::x; # ''
print $Some::y; # '2'

print join(' ', @Some::array); # 'foo bar baz'
\end{minted}
Ключевое слово \verb|our| может также использоваться по отношению к массивам и хешам. В этом случае массив будет доступен по короткому имени внутри пакета, как будто это локальная переменная. Объявленные таким образом переменные будут все ещё доступны снаружи пакета по полным именам.

%\subsection{my,state} %16 (18:28)
Следует напомнить, что \verb|my| задает переменную в локальной области видимости:
\begin{minted}{perl}
my $x = 4;
{
  my $x = 5;
  print $x; # 5
}
print $x; # 4
\end{minted}
Если локальная переменная имеет то же имя, что и некоторая переменная из более широкой области видимости, то последняя в локальной области видимости оказывается недоступной.

С помощью конструкции
\begin{minted}{perl}
use feature 'state';
\end{minted}
можно включить возможность определять переменные с помощью ключевого слова \verb|state|. Основное отличие от \verb|my| состоит в том, что присваивание переменной значения происходит только раз за все время исполнения программы:
\begin{minted}{perl}
sub test {
  state $x = 42;
  return $x++;
}

printf(
  '%d %d %d %d %d',
  test(), test(), test(), test(), test()
); # 42 43 44 45 46
\end{minted}
В данном примере в функции \verb|state| переменной $\$x$ значение 42 присваивается только при первом ее вызове.

\section{Глобальная область видимости} %17 (20:04)
Глобальная область видимости является пакетом с именем \verb|main| (или имя которого~--- пустая строка). Например, принудительно использовать переменную из глобальной области видимости можно указав в качестве имени пакета \verb|main|:
\begin{minted}{perl}
our $size = 42;

sub print_size {
  print $main::size;
}

package Some;
main::print_size(); # 42
\end{minted}
В некоторых пакетах глобальные переменные определяются в начале файла, а потом используются в пакете именно таким образом.

\section{Передача параметов} %18 (20:52)
Функция \verb|use|, кроме того, что исполняется в \verb|BEGIN|, поддерживает передачу параметров. Для этого после имени модуля указывается список параметров, которые будут переданы загрузчику модуля:
\begin{minted}{perl}
use Local::Module ('param1', 'param2');
use Another::Module qw(param1 param2);
\end{minted}
Другими словами, при исполнении \verb|use| не просто выполняется \verb|require| внутри блока \verb|BEGIN|, а также вызвается метод \verb|import| одноименного пакета (если он есть), которому собственно и передаются параметры:
\begin{minted}{perl}
BEGIN {
  require Module;
  Module->import(LIST);
  # ~ Module::import('Module', LIST);
}
\end{minted}
Это еще одна причина использовать одинаковые имена пакетов и модулей: функция \verb|use| будет искать метод \verb|import| в пакете, имя которого совпадает с именем загружаемого модуля.

Если метод \verb|import| вызывать не требуется вообще, после имени модуля нужно указать пустые скобки. Следует отметить, что указание пустых скобок и отсутствие скобок~--- не одно и тоже. Если скобки отсутствуют, метод \verb|import| вызывается без параметров. Пустые скобки~--- прямое указание на запрет вызова \verb|import|.

\section{Экспорт} %19 (23:17)
Название метода \verb|import| исходит из того, что в начале он использовался, чтобы выборочно импортировать некоторые функции из пакета. Например, в модуле \verb|File::Path|, одном из основных модулей \verb|perl|, есть функции $make\_path$ (создать множество вложенных каталогов) и $remove\_tree$ (удалить множество каталогов). Если включить \verb|use File::Path| и в качестве параметров передать \verb|qw(make_path remove_tree)|, то в текущем пакете (в данном случае \verb|main|) появятся функции $make\_path$ и $remove\_tree$:
\begin{minted}{perl}
package My::Package;

use File::Path qw(make_path remove_tree);

# File::Path::make_path
make_path('foo/bar/baz', '/zug/zwang');
File::Path::make_path('...');
My::Package::make_path('...');

# File::Path::remove_tree
remove_tree('foo/bar/baz', '/zug/zwang');
File::Path::remove_tree('...');
My::Package::remove_tree('...');
\end{minted}
После этого, так как метод \verb|import| был написан соответствующим образом, можно будет обращаться к данным функциям не только по полным именам, но и по коротким. Это самый простой и распространённый способ экспорта функций, поскольку позволяет контролировать информацию о полученных функциях и избегать конфликта имен с функциями из других модулей.

Поскольку все модули используют такой механизм, он реализован в отдельном модуле \verb|Exporter|:
\begin{minted}{perl}
package Local::Multiplier;

use Exporter 'import';
our @EXPORT = qw(m2 m3 m4 m5 m6);

sub m2 { shift() ** 2 }
sub m3 { shift() ** 3 }
sub m4 { shift() ** 4 }
sub m5 { shift() ** 5 }
sub m6 { shift() ** 6 }
\end{minted}
В пакете объявляется массив функций, которые требуется экспортировать, вызывается через \verb|use Exporter|, и, как только где-то будет вызван данный модуль, все функции, указанные в массиве, будут экспортированы.
\begin{minted}{perl}
use Local::Multiplier;

print m3(5); # 125
print Local::Multiplier::m3(5); # 125
\end{minted}

Чтобы иметь возможность выбирать, какие функции будут экспортированы, следует использовать массив $@EXPORT\_OK$:
\begin{minted}{perl}
package Local::Multiplier;

use Exporter 'import';
our @EXPORT_OK = qw(m2 m3 m4 m5 m6);

sub m2 { shift() ** 2 }
sub m3 { shift() ** 3 }
sub m4 { shift() ** 4 }
sub m5 { shift() ** 5 }
sub m6 { shift() ** 6 }
\end{minted}
В нем указываются функции, которые могут быть экспортированы. Какие из них будут экспортированы, выбираются по короткому имени при вызове данного модуля.
\begin{minted}{perl}
use Local::Multiplier qw(m3);

print m3(5); # 125
print Local::Multiplier::m4(5); # 625
\end{minted}

Также модуль \verb|Exporter| поддерживает экспорт групп функций. Для этого необходимо задать хеш $\%EXPORT\_TAGS$:
\begin{minted}{perl}
our %EXPORT_TAGS = (
  odd  => [qw(m3 m5)],
  even => [qw(m2 m4 m6)],
  all  => [qw(m2 m3 m4 m5 m6)],
);
\end{minted}
Чтобы экспортировать из модуля определенную группу функций используется синтаксис с двоеточием:
\begin{minted}{perl}
use Local::Multiplier qw(:odd);

print m3(5);
\end{minted}
К данному моменту уже перечислены основные механизмы подключения модулей.

\section{Загрузка определенной версии пакета}
Perl поддерживает загрузку пакета строго определенной версии. Для этого требуемую версию необходимо указать сразу после имени пакета:
\begin{minted}{perl}
use File::Path 2.00 qw(make_path);
\end{minted}
Версия пакета указывается внутри пакета в специальной переменной $\$VERSION$ следующим образом:
\begin{minted}{perl}
package Local::Module;

our $VERSION = 1.4;
\end{minted}
Если в это переменной будет значение меньше запрашиваемого, будет выведено сообщение об ошибке:
\begin{minted}{perl}
use Local::Module 1.5;
\end{minted}
\begin{minted}{bash}
$ perl -e 'use Data::Dumper 500'
Data::Dumper version 500 required--
this is only version 2.130_02 at -e line 1.
BEGIN failed--compilation aborted at -e line 1.
\end{minted}

На самом деле при проверке версии пакета вызвается метод \verb|VERSION| этого пакета: %24 29:19
\begin{minted}{perl}
use Local::Module 500;
# Local::Module->VERSION(500);
# ~ Local::Module::VERSION('Local::Module', 500);
\end{minted}
Внутри модуля эту функцию можно определить произвольным образом и тем самым задать то, как происходит проверка версии.
\begin{minted}{perl}
package Local::Module;

sub VERSION {
  my ($package, $version) = @_;

  # ...
}
\end{minted}
Такая функциональность требуется крайне редко.

Уже достаточно давно в \verb|perl| присутствует синтаксис для так называемых \verb|version strings|:
\begin{minted}{perl}
use Local::Module v5.11.133;
\end{minted}
Синтаксис состоит в том, что после имени модуля ставится символ <<v>> и сразу за ним несколько чисел, разделенных точками.
\begin{minted}{perl}
v102.111.111; # 'foo'
102.111.111;  # 'foo'
v1.5;
\end{minted}

Такая запись превращается в последовательность символов. Количество символов в последовательности равняется количеству чисел, а каждый символ в строке таков, что его код равен соответствующему числу. Такое преобразование версии в строку позволяет производить корректное лексикографическое сравнение двух версий. При лексикографическом сравнении сначала сравниваются первые символы двух строк, потом вторые и так далее. Так и при сравнении двух номеров версий сначала будут сравниваться старшие номера версий, и, если они совпадают, следующий за старшим и так далее.

Об этом не стоило бы и говорить, если бы не одно с этим связанное недоразумение. Дело в том, что если переменные или ключи хешей похожи на v-string, интерпретатор может принять эту запись за v-string и сделать вышенаписанное преобразование. Это следует иметь в виду и брать v и числа в кавычки, поскольку иначе запись будет неправильно воспринята интерпретатором.

\section{Указание версии интерпретатора}
При использовании команды \verb|use| можно указать номер версии, но не указывать название пакета. В таком случае будет указана требуемая версия интерпретатора \verb|perl|, а также станут доступны все возможности, которые появились в этой версии:
\begin{minted}{perl}
use 5.12.1;
use 5.012_001;

$^V # v5.12.1
$]  # 5.012001
\end{minted}
Версия интерпретатора хранится в переменной $\$\^V$ (в новом формате) и переменной $\$]$ (в старом формате):
\begin{minted}{perl}
use Module v1.1.1;
use 5.10;
\end{minted}
Именно с этой версией и будет сравниваться указанная после \verb|use| версия.

\section{Pragmatic modules} %27 31:35
С помощью \verb|use| можно загружать так называемые \verb|pragmatic modules|. От обычных модулей они отличаются тем, что (условно) влияют на ход интерпретации программы и их имена традиционно начинаются с маленькой буквы. Однако, строго говоря, какой-то конкретной границы между такими и обычными модулями нет.

Чаще всего используются два pragmatic модуля: \verb|strict| и \verb|warnings|.
\begin{minted}{perl}
use strict;
use warnings;
\end{minted}
Модуль \verb|strict| позволяет включать дополнительные ограничения, а модуль \verb|warnings| включает предупреждения.

\subsection{Модуль strict} %28 33:24
Если параметры не указаны, модуль \verb|strict| включает все три доступных типа ограничений. Фактически, \verb|use strict|~--- это \verb|use strict 'refs'|, \verb|use strict 'vars'|, \verb|use strict 'subs'| вместе взятые. В результате этого некоторые опасные возможности языка становятся недоступными для программиста. Код же, который не использует эти возможности, становится более чистым и надежным.

Использование \verb|use strict 'refs'| позволяет избежать следующей нежелательной ситуации. Следует напомнить, что, если разыменовать указатель на переменную, то получается значение этой переменной:
\begin{minted}{perl}
use strict 'refs';

$ref = \$foo;
print $$ref;  # ok
$ref = "foo";
print $$ref;  # runtime error; normally ok
\end{minted}
Если не указано \verb|use strict 'refs'|, то результатом разыменования переменной-строки будет значение переменной, имя которой есть данная строка (строка может, например, быть считана из стандартного ввода или стороннего файла). Это немного странно и \verb|use strict 'refs'| запрещает такое поведение. Однако иногда такое поведение необходимо, поэтому этот режим можно отключить (об этом будет сказано позднее).

%\subsection{use strict 'vars'} %29 (34:43)
С помощью \verb|use strict 'vars'| можно потребовать явной инициализации переменной с помощью ключевых слов \verb|my| или \verb|our|. Если \verb|use strict 'vars'| не использовать, то обращение (без указания \verb|my| или \verb|our|) в начале файла к переменной $\$x$, фактически, будет обращением к переменной $\$main::x$ (к глобальной переменной), а не к локальной переменной, как это, возможно, задумывалось.

% WARNING: Может поменять местами куски?
С помощью \verb|use strict 'subs'| отключается автоматические перевод bareword'ов (слово без кавычек) в строки:
\begin{minted}{perl}
use strict 'vars';
$Module::a;
my  $x = 4;
our $y = 5;
\end{minted}
Например, если \verb|use strict 'subs'| не используется и функция $test$ не определена:
\begin{minted}{perl}
use strict 'subs';
print Dumper [test]; # 'test'
\end{minted}
Если же до этого определить функцию $test$, поведение совершенно меняется:
\begin{minted}{perl}
sub test {
  return 'str';
}
print Dumper [test]; # 'str'
\end{minted}
Подход, в котором то, как будет интерпретироваться bareword, зависит от того, какие функции существуют на момент исполнения кода, является неприемлемым (кроме, может быть, в случае однострочников).

\subsection{Модуль warnings} %37:10
Модуль \verb|warnings| включает отображение предупреждений только в данной области видимости (в отличие от ключа <<w>> интерпретатора):
\begin{minted}{perl}
use warings;
use warnings 'deprecated';
\end{minted}
Использовать модуль \verb|warnings| более правильно, так как он не включает предупреждения в модулях, где предупреждения могли быть сознательно проигнорированы автором модуля.
\begin{minted}{bash}
$ perl -e 'use warnings; print(5+"a")'
Argument "a" isn't numeric in addition (+) at -e line 1.
\end{minted}

Другой модуль \verb|diagnostics| аналогичен модулю \verb|warnings|, но также выводит подробную инфомацию по каждому предупреждению:
\begin{minted}{bash}
$ perl -e 'use diagnostics; print(5+"a")'
Argument "a" isn't numeric in addition (+) at -e line 1 (#1)
    (W numeric) The indicated string was fed as an argument to an operator
    that expected a numeric value instead.  If you're fortunate the message
    will identify which operator was so unfortunate.
\end{minted}
Это может быть особенно полезно новичкам в \verb|perl|. Использовать же его в production не стоит, так как человек который будет разбираться с предупреждением сможет самостоятельно найти помощь по этой ошибке в интернете.

\subsection{Модули lib и FindBin} %38:30
Модуль \verb|lib| позволяет добавить путь к массиву $@INC$, который содержит директории, в которых будут будет производиться поиск модулей. Вместо того, чтобы вручную добавлять путь с помощью команды \verb|unshift| в блоке \verb|BEGIN|:
\begin{minted}{perl}
use lib qw(/tmp/lib);

BEGIN { unshift(@INC, '/tmp/lib') }
\end{minted}
можно просто воспользоваться этим модулем.

В связке с этим модулем используется модуль \verb|FindBin|, который позволяет сохранить путь к текущему бинарному файлу в некоторой переменной. После этого можно указывать в модуле \verb|lib| путь относительно пути к бинарному файлу:
\begin{minted}{perl}
use FindBin '$Bin';
use lib "$Bin/../lib";
\end{minted}
Обычно так работают standalone-программы.

\subsection{Модуль feature} %40 Опечатка на слайде
Модуль \verb|feautre| позволяет подключить возможность, добавленную в поздних версиях \verb|perl| и которая не была сделана возможностью по умолчанию, например, чтобы избежать конфликта имен. Следующий код подключает функцию \verb|say|, которая отличается от \verb|print| тем, что дополнительно делает перевод строки:
\begin{minted}{perl}
use feature qw(say);

say 'New line follows this';
\end{minted}
Если программист уже определил функцию \verb|say|, то добавление этой функции по умолчанию приведет к конфликту имен.

\subsection{Модуль brignum} %40:13
Модули \verb|bigint| и \verb|bigrat| позволяют отключить встроенное ограничение на длину вычисляемого значения для целых и рациональных чисел соответственно. Например, \verb|bigint| отключает округление при больших значениях целочисленной переменной:
\begin{minted}{perl}
use bignum;
use bigint;
use bigrat;
\end{minted}
\begin{minted}{bash}
$ perl -E 'use bigint; say 500**50'
888178419700125232338905334472656250000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000

$ perl -E 'say 500**50'
8.88178419700125e+134
\end{minted}
Модуль \verb|bignum| подключает оба модуля сразу.

\subsection{Отключение модулей} %43
С помощью \verb|no|, антипода \verb|use|, можно отключить в данный момент не нужные модули. В этом случае вместо метода \verb|import| (в \verb|use|) используется метод \verb|unimport|.
\begin{minted}{perl}
no Local::Module LIST;

# Local::Module->unimport(LIST);
\end{minted}
С помощью \verb|no| можно отключить возможности, которые были добавлены в современных версиях \verb|perl|:
\begin{minted}{perl}
no 5.010;
\end{minted}
Также с помощью \verb|no| можно отключить pragmatic модули, в частности \verb|strict| и \verb|feature|:
\begin{minted}{perl}
no strict;
no feature;
\end{minted}
Обычно эти возможности используются, чтобы локально (в отдельной области видимости) выключить одно из ограничений, накладываемое \verb|strict|, и сделать то, что это ограничение запрещает. После закрывающей фигурной скобки все локально выключенные ограничения вновь будут в силе. Такой подход позволяет использовать потенциально опасные операции только в рамках локальной области видимости и осознанно.


\section{Внутренние механизмы работы perl}
\subsection{Symbol Tables} %42:30
%\subsection{Typeglob}
В \verb|perl| для каждого загруженного пакета создается специальный служебный хеш. Он имеет имя, которое состоит из символа процента, затем имени пакета и двойного двоеточия за ним. Например, если был загружен модуль \verb|Data::Dumper|, то станет доступным хеш \verb|\%Data::Dumper::|.
\begin{minted}{perl}
{
  package Some::Package;
  our $var = 500;
  our @var = (1,2,3);
  our %func = (1 => 2, 3 => 4);
  sub func { return 400 }
}
\end{minted}
Внутри этого хеша можно увидеть так называемую символическую таблицу. Для модуля:
\begin{minted}{perl}
use Data::Dumper;
print Dumper \%Some::Package::;
\end{minted}
соответствующая символическая таблица имеет вид:
\begin{minted}{perl}
$VAR1 = {
          'var' => *Some::Package::var,
          'func' => *Some::Package::func
        };
\end{minted}

% TODO Путается

\subsection{Typeglob} %49 44:30

% TODO Путается

\subsection{Функция caller} %50
Встроенная функция \verb|caller| позволяет получить данные из стека вызовов. Если эта функция была вызвана без параметров, то она вернет название пакета, откуда была вызвана текущая функция, соответствующие имя файла и номер строчки:
\begin{minted}{perl}
# 0         1          2
($package, $filename, $line) = caller();
\end{minted}
В качестве параметра можно указать глубину. В этом случае \verb|caller| вернет гораздо больше информации, в том числе, в каком контексте была вызвана функция и так далее:
\begin{minted}{perl}
(
	$package,    $filename,   $line,
	$subroutine, $hasargs,    $wantarray,
	$evaltext,   $is_require, $hints,
	$bitmask,    $hinthash
) = caller($i);
\end{minted}
\verb|Export| работает именно так: через функцию \verb|caller| узнает имя пакета, откуда он был вызван, а затем с помощью операций над таблицами-символами создаёт нужную функцию в этом пакете.

\subsection{Перехват обращения к несуществующей функции} %51 :48:19
В \verb|perl|, как и во многих других современных интерпретируемых языках, есть способ перехватывать обращения к несуществующим функциям. До того, как будет брошено исключение о том, что запрашиваемой функции нет, будет предпринята попытка вызвать функцию \verb|AUTOLOAD| из этого пакета:
\begin{minted}{perl}
\end{minted}
В переменной пакета $\$AUTOLOAD$ будет лежать имя той функции, которая пыталась быть вызвана. Стоит отметить, что в качестве параметров функции \verb|AUTOLOAD| передаются параметры вызываемой функции.

Это позволяет объявлять не одну функцию, а сразу класс функций. Для тех, кто знаком с интерпретируемыми языками, это механизм уже может быть знаком. Например, в Ruby это называется missing method.

\subsection{Ключевое слово local} %52 50:02
Помимо \verb|my|, \verb|state| и \verb|our|, есть еще похожее на них ключевое слово \verb|local|, которое, однако, не имеет с ними ничего общего. В \verb|perl| существует возможность временно присвоить любой переменной некоторое значение до конца области видимости:
\begin{minted}{perl}

{
  package Test;
  our $x = 123;

  sub bark { print $x }
}

Test::bark(); # 123
{
  local $Test::x = 321;
  Test::bark(); # 321
}
Test::bark(); # 123
\end{minted}
Чаще всего это используется в тех случаях, когда требуется временно поменять поведение и восстановить старое поведение после.

В частности, можно временно изменить значения служебных переменных. Поскольку эти переменные используются внутренними механизмами \verb|perl|, их прежнее значение должно быть возвращено. Примером использование данной возможности является переопределение служебной переменной, которая определяет перенос конца строки, чтобы считывать файлы не построчно, а целиком.

На самом деле с помощью \verb|local| можно переопределять не только переменные, но даже ключи в хеше. Более того, существует конструкция \verb|delete local|, которая удалит ключ, но только локально до конца области видимости, а потом вернет его на место. Возможности \verb|local| безграничны, но рекомендуется не злоупотреблять им, потому что в сложных конструкциях его действие может быть не очевидно:
\begin{minted}{perl}
# localization of values
local $foo;
local (@wid, %get);
local $foo = "flurp";
local @oof = @bar;
local $hash{key} = "val";
delete local $hash{key};
local ($cond ? $v1 : $v2);

# localization of symbols
local *FH;
local *merlyn = *randal;

local *merlyn = 'randal';
local *merlyn = \$randal;
\end{minted}
Подробную справку по ключевому слову \verb|local| можно найти в документации.

\setcounter{chapter}{5}
\chapter{Объектно-ориентированное программирование}
\section{Введение: объекты, методы и атрибуты} % %3 01:59
ООП~--- самая распространенная на сегодня парадигма программирования. Исторически ООП в Perl не было и оно было добавлено позже. Объект~--- данные и методы работы с ними.
В Perl уже существуют структуры данных (массивы, хеши, и их комбинации), а также наборы поведений (пакеты). Остается объединить эти две сущности.

Ключевое слово \verb|bless| именно это и делает~--- превращает структуру данных в объект с методами из некоторого пакета:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-bless.pl}

Объект можно сделать из любого ref-а (скаляр, массив или хеш),
но, как правило, используются только ссылки на хеш.
Настоятельно рекомендуется использовать именно этот способ.
В хеш можно положить разнообразные данные в отличие от скаляра или массива,
поэтому использование ссылки на хеш не накладывает дополнительных ограничений;

Из объекта можно вызвать метод с помощью стрелочки \verb'->',
за которой следуют имя метода и его аргумент:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-method.pl}

Реально произойдет следующее: из пакета будет вызвана функция, причем первым аргументом будет передана ссылка на объект, из которого был вызван метод. Таким образом устанавливается обратная связь между методом и объектом.
Все остальные аргументы передаются как и при обычном вызове.
Во многих языках программирования имеется возможность обращения метода к объекту, из которого его вызвали, с помощью ключевого слова. В Perl такое ключевое слово традиционно обозначается \verb|$self|. Несмотря на то, что имеется возможность задать ключевому слову собственное обозначение, большого смысла в этом нет.

Помимо методов объект наделен атрибутами. Если объект был сделан из хеша, то значения в хеше можно считать атрибутами, а ключи~--- именами атрибутов.

В Perl нет ключевого слова \verb|new|. Но для удобства можно определить метод, который будет вести себя похожим образом:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-new.pl}

В следующем примере метод \verb|new| вызывается из пакета.
Такой метод называется методом класса:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-class.pl}

В этом случае в качестве первого параметра будет передана строка с именем пакета
(такой аргумент принято называть \verb|$class|, а не \verb|$self|).
Методы класса синтаксически ничем не отличаются от методов объекта, кроме как способом вызова.
Особо следует отметить, что в \verb|$class| могут попадать и имена наследников, если используется наследование.

Существует еще один способ вызова методов~--- после стрелочки явно указать полное имя функции
\footnote, которую нужно вызвать, вместе с именем пакета:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-fqn.pl}

Важно уточнить, что все объявленные методы~--- функции из пакета.
Они все еще могут быть вызваны через <<\verb|::|>>,
но в этом случае не будет передано имя класса и не будет работать механизм наследования.

В Perl существует возможность создавать объекты похожим на C++ образом:
\begin{minted}[frame=leftline,framesep=1em,linenos,firstline=1]{perl}
new My::Class(1, 2, 3);

My::Class->new(1, 2, 3);
\end{minted}

Для этого был придуман \textbf{indirect method call}, который позволяет вызвать любой метод следующим образом:
\begin{minted}[frame=leftline,framesep=1em,linenos,firstline=1]{perl}
foo $obj(123);  # $obj->foo(123);
\end{minted}

Рекомендуется никогда не использовать этот метод. Есть замечательный пример, почему так делать не стоит:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-syntax-error.pl}
Этот пример проходит проверку синтаксиса, хотя совершенно не похож на перловый код.
Здесь из класса \verb|error| вызывается метод \verb|Syntax|, для которого
восклицательный знак и \verb|exit 0;| являются параметром:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-syntax-error-desc.pl}
Поскольку параметры вычисляются до вызова функции, то ещё до того, как произойдет
вызов \verb|'error'->Syntax| вычисляется параметр \verb|exit 0;|,
который и завершает программу с успешным кодом возврата \verb|0|.
Ошибку о том, что в пакете \verb|error| нет метода \verb|Syntax|,
Perl вывести не успевает, так как программа завершает исполнение раньше.

Во всех классах определён метод \verb|can|:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-can.pl}
Метод \verb|can| позволяет проверить, может ли использоваться метод,
имя которого передано в параметрах.
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-can-bless.pl}
Метод \verb|can| возвращает ref метода в случае, если метод существует:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-can-ref.pl}
Здесь передается строка \verb|'A'| в качестве параметра,
так как \verb|can| возвращает уже не метод, а функцию.

Метод \verb|can| можно вызывать не только из классов, но и из объектов:
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-can-bless.pl}

\textbf{Filehandle}~--- тоже объект, у которого есть, в том числе, следующие методы:
\begin{itemize}
  \item \verb|autoflush|~--- сброс буфера,
  \item \verb|print|~--- печать
\end{itemize}

\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-filehandle.pl}
Соответственно, предустановленные \verb|STDOUT| и \verb|STDERR|~--- тоже объекты,
и из них можно вызывать методы.

На прошлой лекции не было сказано, почему при вызове \verb|use| и \verb|no| в качестве первого аргумента передается имя пакета.
Теперь понятно, что на самом деле вызывается функция из пакета, первым параметром которой является имя пакета.
\inputminted
    [frame=leftline,framesep=1em,linenos,firstline=1]
    {perl}
    {lectures/L6/L6-example-intro-use.pl}


\section{Примеры использования ООП}% 22:54
\subsection{Модуль DBI}
Модуль \textbf{DBI} (database interface) задаёт интерфейс работы с абстрактной базой данных. От него наследуются все реализации движков конкретных баз данных. Метод DBI->connect позволяет задать параметры подключения и возвращает  \textbf{dbh} (database handler):
\begin{minted}{perl}
$dbh = DBI->connect(
  $data_source,
  $username,
  $auth,
  \%attr
);

$rv = $dbh->do('DELETE FROM table');
\end{minted}
Метод \textbf{do} объекта \textbf{dbh} позволяет исполнять запросы. Он вернёт указатель на следующий объект, из которого уже можно будет вычислить результат.


\subsection{XML::LibXML}%16 23:56
Библиотека \textbf{XML::LibXML}~--- работает с системами библиотек LibXML и используется для парсинга XML:
\begin{minted}{perl}
use XML::LibXML;

my $document = XML::LibXML->load_xml(
    string => '...'
);

my $list = $document->findnodes('...');
    # XML::LibXML::NodeList
\end{minted}

Метод \verb|load_xml| парсит XML из строчки в xml-объект, у которого есть методы для работы с XML. Например, \verb|findnodes| возвращает список nodes определённого типа. В этой библиотеке широко используется наследование:
\begin{verbatim}
       XML::LibXML::Node
      /                 \
 XML::LibXML::Document  XML::LibXML::Element
\end{verbatim}
% TODO про производительность

\subsection{File::Spec}%17 25:38
Базовый модуль \textbf{File::Spec} содержит метод \textbf{catfile}, который склеивает из кусков пусть к файлу:
\begin{minted}{perl}
use File::Spec;

print File::Spec->catfile('a', 'b', 'c');
\end{minted}
\textbf{File::Spec} автоматически убирает дублирующие слеши, а также учитывает особенности используемой операционной системы.

\subsection{JSON} %18 26:37
У модуля JSON, помимо необъектного существует объектный интерфейс.
Каждый его метод возвращает одну и ту же ссылку, но меняет при этом внутренние атрибуты.
\begin{minted}{perl}
use JSON;

JSON->new->utf8->decode('...');

decode_json '...';
\end{minted}


\section{Наследование}%20 28:59
Класс Рысь (Lynx) наследуется от классов собака и кошка (ввиду похожести). В специальном массиве \textbf{@ISA} хранятся родители класса. Это переменная пакета, куда можно положить имена родителей, и пакет будет немедленно наследован от них. Блок \textbf{BEGIN} стоит для того, чтобы наследование происходило до исполнения кода.
\begin{minted}{perl}
{
  package Lynx;

  BEGIN { push(@ISA, 'Dog', 'Cat') }

}
\end{minted}
Альтернативно можно использовать любую из двух прагм:
\begin{minted}{perl}
  use base qw(Dog Cat);
  use parent qw(Dog Cat);
\end{minted}
В новых проектах рекоментуется использовать более современную прагму parent, но если в проекте уже используется base~--- рекомендуется продолжать его использовать.

В perl поддерживается, как можно догадаться, множественное наследование: поиск метода будет производиться у родителей в том порядке как они были указаны при наследовании.

Класс UNIVERSAL является супер-родителем всех остальных классов:
\begin{minted}{perl}
$obj->can('method');

$obj->isa('Animal');
Dog->isa('Animal');

$obj->VERSION(5.12);
\end{minted}
Именно в этом классе определены методы can, VERSION и isa. Метод isa возвращает true, если объект принадлежит классу, имя которого передано в качестве аргумента, или любому его наследнику.

В каждом классе существует специальный псевдокласс SUPER, указывающий на родителя. Так можно у наследника вызвать метод родителя, который был переопределен при наследовании:
\begin{minted}{perl}
sub foo {
  my ($self, %params) = @_;

  $self->SUPER::foo(%params);

  return;
}
\end{minted}
Порядок разрешения методов будет рассмотрен позже.

\section{Method Resolution Order}% 34:32
В момент, когда множественное наследование только появилось, немедленно выяснилось, что порядок, в котором будет происходить поиск метода по дереву, далеко неочевиден.

Существует простой и очевидный алгоритм поиска, который используется в Perl~--- просто подряд перебирать родителей класса, которые, в свою очередь, перебирают своих:
\begin{verbatim}
      Animal
        |
      Pet   Barkable
      /   \   /
      Cat   Dog
      \   /
      Lynx
\end{verbatim}
В результате вызова метода:
\begin{minted}{perl}
Lynx->method();
\end{minted}
поиск будет осуществляться в следующем порядке:
\begin{minted}{perl}
qw(Lynx Cat Pet Animal Dog Barkable);
\end{minted}
У этого подхода есть серьезная проблема: если в классе Pet метод класса был переопределен, он будет использоваться, вне зависимости от того, был ли он переопределен у Dog. Очевидно, такое поведение не подходит.

Чтобы решить эту проблему есть алогритм \textbf{c3}, который не идет наверх, пока не будут опрошены все дочерние классы:
\begin{minted}{perl}
use mro 'c3';
Lynx->method();
qw(Lynx Cat Dog Pet Animal Barkable);
\end{minted}
Такой порядок поиска метода гораздо более полезен. Включить такой способ поиска методов можно с помощью  \textbf{use mro 'c3'}. После этого (дополнительно к псевдопакету SUPER) появится псевдопакет next, который отличается от SUPER тем, что метод будет искаться по алгоритму \textbf{c3}. Следует особо отметить, что при использовании next не нужно повторять имя метода:
\begin{minted}{perl}
package A;
use mro;

sub foo {
  my ($self, $param) = @_;

  $param++;

  return $obj->next::method($param);
}
\end{minted}

\section{Детали} %28 40:16
Встроенная функция \textbf{ref} сообщает то, на что идет ссылка:
\begin{minted}{perl}
use JSON:

ref JSON->new(); # 'JSON'
ref [];          # 'ARRAY'
ref {};          # 'HASH'
ref 0;           # ''
\end{minted}
Для объектов она возвращает имя класса, к которому принадлежит объект. Часто бывает удобнее использовать функцию blessed:
\begin{minted}{perl}
use JSON:
use Scalar::Util 'blessed';

blessed JSON->new(); # 'JSON'
blessed [];          # undef
blessed {};          # undef
blessed 0;           # undef
\end{minted}
Функция blessed на объектах ведет себя как ref, а во всех остальных случаях возвращает undef.

В классах, как уже было сказано, есть метод can, который говорит, есть ли какие-то методы в классе или нет. Если какие-то методы были определены через \textbf{AUTOLOAD}, то для can они будут невидимы:
\begin{minted}{perl}
package A;
our $AUTOLOAD;
sub new {
  my ($class, %params) = @_;
  return bless \%params, $class;
}
sub AUTOLOAD { print $AUTOLOAD }
\end{minted}
\begin{minted}{perl}
A->new()->test(); # test
A->can('anything'); # :(
\end{minted}
Соответственно, если переопределяется AUTOLOAD, необходимо также переопределить can. Порядок поиска AUTOLOAD следующий:
\begin{minted}{perl}
sub UNIVERSAL::AUTOLOAD {}

# Dog->m(); Animal->m(); UNIVERSAL->m();
# Dog->AUTOLOAD(); Animal->AUTOLOAD();
# UNIVERSAL->AUTOLOAD();
\end{minted}
Интересно, что можно переопределить \verb|UNIVERSAL->AUTOLOAD|, который будет перехватывать вызовы всех несущетсвующих функций.

В perl нет конструкторов, <<конструктор>> обычно реализуется вручную как метод класса, обычно имеющий название new (обычно достаточно один раз определить такой конструктор для того класса, от которого будут наследоваться все классы в проекте). Но в perl есть деструкторы.
\begin{minted}{perl}
package A;
sub new {
  my ($class, %params) = @_;
  return bless \%params, $class;
}

sub DESTROY {
  my ($self) = @_;
  print 'D';
}
\end{minted}
Для того, чтобы создать деструктор, необходимо объявить метод DESTROY. Он будет вызываться при уничтожении объекта, когда на него закончатся ссылки.
\begin{minted}{perl}
A->new(); # print 'D'
\end{minted}
В этом примере созданный объект был сразу же уничтожен, поскольку не был сохранен в переменную.

При использовании деструкторов возникают следующие сложности:
\begin{itemize}
  \item Если внутри деструктора сделать die, программа не упадет, но приведет к изменению переменной \verb|$@|.

  \item В деструкторе нужно быть максимально аккуратным при работе с глобальными переменными. Для всех служебных переменных следует использовать local (локализовать), чтобы неявный вызов деструктора не приводил к нежелательному изменению глобальных переменных. Особенно аккуратно нужно быть с использованием регулярных выражений, так как соответствующие им глобальные переменные будут изменены и это может оказать влияние на исполняющийся в данный момент код.

  \item Существует следующая проблема с AUTOLOAD. В документации написано, что если DESTROY (если он не объявлен) попадает в AUTOLOAD. Но начиная с версии 5.18 это поведение было сломано в результате оптимизации производительности: DESTROY теперь не попадает в AUTOLOAD. Но это и не должно стать проблемой, так как хорошим тоном считается объявлять пустой DESTROY, если в классе объявлен AUTOLOAD.

  \item Когда программа завершается, вызываются все деструкторы всех объектов, которые остались в памяти, причем порядок вызова этих объектов не определен. Может получиться ситуация, что в деструкторе какого-то объекта будут обращения к объектам, которые уже уничтожены. Обычно используется следующее решение:
\begin{minted}{perl}
  sub DESTROY {
    my ($self) = @_;
    $self->{handle}->close() if $self->{handle};
  }
\end{minted}
  Также существует следующий хак: \verb|${^GLOBAL_PHASE} eq 'DESTRUCT'|
\end{itemize}

Поскольку атрибуты объектов~--- элементы хеша, и перехватить обращение к ним представляет сложную задачу, имеет смысл работать с ними с помощью специально созданных для этого методов (сеттеры и геттеры). Некоторые модули позволяют делать это автоматически:
\begin{minted}{perl}
package Foo;
use base qw(Class::Accessor);
Foo->follow_best_practice;
Foo->mk_accessors(qw(name role salary));
\end{minted}
Метод \verb|follow_best_practice| приводит к тому, что получившиеся методы будут называться \verb|set_<name>| и \verb|get_<name>|. Если его не вызывать, то метод на каждый атрибут будет один и без параметра он будет работать как getter, а с параметром~--- как сеттер.

Существуют также более быстрые реализации этого пакета:
\begin{minted}{perl}
use base qw(Class::Accessor::Fast);
use base qw(Class::XSAccessor);
\end{minted}
Рекомендуется использовать Class::XSAccessor, так как он написан на Си и работает очень быстро.

\section{Moose-like} %34 52:04
Поскольку реализация ООП в perl неудобна, появился такой продукт как Moose. Этот модуль стал де-факто стандартом реализации ООП в perl. Первое, что позволяет Moose, это добавление <<атрибутов>> в классы:
\begin{minted}{perl}
package Person;

use Moose;

has first_name => (
  is  => 'rw',
  isa => 'Str',
);

has last_name => (
  is  => 'rw',
  isa => 'Str',
);
\end{minted}
По сути это те же самые сеттеры и геттеры с гораздо более изящным и изощренным синтаксисом. При задании атрибута с помощью has необходимо указать в is, будет ли меняться значение атрибута на лету, а также в isa указать тип атрибута. В Moose уже определен свой метод new и переопределять его нельзя:
\begin{minted}{perl}
Person->new(
  first_name => 'Vadim',
  last_name  => 'Pushtaev',
);
\end{minted}
Наследование происходит с помощью ключевого слова extends:
\begin{minted}{perl}
package User;

use Moose;

extends 'Person';

has password => (
  is => 'ro',
  isa => 'Str',
);
\end{minted}
Часто в момент создания объекта необходимо выполнить определенный код. Для этого используется хук внутри конструктора, который исполняет код в \textbf{BUILD}:
\begin{minted}{perl}
has age      => (is => 'ro', isa => 'Int');
has is_adult => (is => 'rw', isa => 'Bool');

sub BUILD {
  my ($self) = @_;

  $self->is_adult($self->age >= 18);

  return;
}
\end{minted}
Есть более изящный метод сделать тоже самое~--- передать ссылку на процедуру при определении атрибута в поле \textbf{default}:
\begin{minted}{perl}
has age      => (is => 'ro', isa => 'Int');
has is_adult => (
  is => 'ro',
  isa => 'Bool',
  lazy => 1,
  default => sub {
    my ($self) = @_;
    return $self->age >= 18;
  }
);
\end{minted}
Здесь также необходимо гарантировать, что \verb|is_adult| будет инициализирована после возраста. Для этого указывается \verb|lazy => 1|: в этом случае \verb|is_adult| будет инициализироваться только тогда, когда понадобится.

Помимо этого можно определить хук в отдельном методе и передать в поле \textbf{builder}:
\begin{minted}{perl}
has age      => (is => 'ro', isa => 'Int');
has is_adult => (
  is => 'ro', isa => 'Bool',
  lazy => 1,  builder => '_build_is_adult',
);

sub _build_is_adult {
  my ($self) = @_;
  return $self->age >= 18;
}
\end{minted}
В отличии от хука с default, метод, на который указывает builder, можно переопределить при наследовании:
\begin{minted}{perl}
package SuperMan;
extends 'Person';
sub _build_is_adult { return 1; }
\end{minted}

Передав в has вместо имени массив имен, можно определить множество атрибутов сразу:
\begin{minted}{perl}
has [qw(
  file_name
  fh
  file_content
  xml_document
  data
)] => (
  lazy_build => 1,
  # ...
);

sub _build_fh           { open(file_name) }
sub _build_file_content { read(fh) }
sub _build_xml_document { parse(file_content) }
sub _build_data         { find(xml_document) }
\end{minted}
С помощью \verb|lazy_build| можно строить цепочки инициализации, где одно поле зависит от другого.

Если есть ряд классов, которые не наследованы от общего родителя, но к ним должно быть добавлено одно и то же поведение. Это поведение называется ролью, которая может быть подмешана к классу:
\begin{minted}{perl}
with 'Role::HasPassword';
\end{minted}
Чтобы объявить роль, необходимо объявить пакет с \verb|use Moose::Role;| вместо \verb|use Moose;|:
\begin{minted}{perl}
package Role::HasPassword;
use Moose::Role;
use Some::Digest;

has password => (
  is => 'ro',
  isa => 'Str',
);

sub password_digest {
  my ($self) = @_;

  return Some::Digest->new($self->password);
}
\end{minted}
Существует механизм делегирования с помощью поля handles:
\begin{minted}{perl}
has doc => (
  is    => 'ro',
  isa   => 'Item',
  handles => [qw(read write size)],
);
\end{minted}
В этом случае методы read, write и size будут вызваны из doc, а не из самого объекта. Существует способ мапить имена:
\begin{minted}{perl}
has last_login => (
  is    => 'rw',
  isa   => 'DateTime',
  handles => { 'date_of_last_login' => 'date' },
);
\end{minted}
И даже использовать регулярные выражения:
\begin{minted}{perl}
{
  handles => qr/^get_(a|b|c)|set_(a|d|e)$/,
  handles => 'Role::Name',
}
\end{minted}

В Moose хуки before и after можно навешивать на поля:
\begin{minted}{perl}
before 'is_adult' => sub { shift->recalculate_age }
\end{minted}
Можно создавать свои типы на основе готовых типов, писать свои проверки и сообщения об ошибках:
\begin{minted}{perl}
subtype 'ModernDateTime'
  => as 'DateTime'
  => where { $_->year() >= 1980 }
  => message { 'The date is not modern enough' };

has 'valid_dates' => (
  is  => 'ro',
  isa => 'ArrayRef[DateTime]',
);
\end{minted}
Даже существует возможность использовать расширения:
\begin{minted}{perl}
package Config;
use MooseX::Singleton; # instead of Moose
has 'cache_dir' => ( ... );
\end{minted}

Так как Moose работает медленно, существует несколько переписанных реализаций модуля:
\begin{itemize}
	\item Moose
    \item Mouse~--- самая быстрая версия, написанная на C++, но имеет, в отличие от Moose, проблемы с расширениями.
    \item Mo~--- работает еще быстрее
    \item M~--- самая легковесная версия
\end{itemize}
Наиболее востребованные версии~--- Mouse и Mo.
